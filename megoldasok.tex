% -*- fill-column: 52 -*-
% (local-set-key (kbd "C-c C-f") 'display-fill-column-indicator-mode)

\chapter{A feladatok megoldásai}
\subsubsection*{1.~feladat}
\begin{query}
?- szülő(huszajn, X).
false

?- szülő(X, huszajn).
X = ali ;
X = fátima

?- szülő(ámna, X), szülő(X, fátima).
X = mohamed

?- szülő(ámna, X), szülő(X, Y), szülő(Y, haszan).
X = mohamed,
Y = fátima
\end{query}
\subsubsection*{2.~feladat}
\begin{query}
?- szülő(X, ali).
?- szülő(umáma, X).
?- szülő(X, zajnab), szülő(Nagyszülő, X).
\end{query}
\subsubsection*{4.~feladat}
\begin{program}
boldog(X) :- vangyereke(X).
kétgyerekes(X) :- szülő(X, Y), fivér(Y, _).
\end{program}
\subsubsection*{5.~feladat}
\begin{program}
unoka(X, Y) :- nagyszülő(Y, X).
\end{program}
\subsubsection*{6.~feladat}
\begin{program}
nővér(X, Y) :-
    nő(X),
    szülő(Z, X), szülő(Z, Y),
    X \= Y.
nagynéni(X, Y) :- nővér(X, Z), szülő(Z, Y).
\end{program}
\begin{query}
?- nagynéni(zajnab, X), férfi(X).
\end{query}
\subsubsection*{7.~feladat}
Jó a definíció; X őse Z-nek, ha (i) X szülője Z-nek, vagy (ii) X őse Z szülőjének.
\subsubsection*{8.~feladat}
Változó; atom; atom; változó; atom; struktúra; szám; (hibás); struktúra; (hibás).
\subsubsection*{9.~feladat}
Erre a feladatra nincs \emph{egyetlenegy} jó megoldás; különböző alkalmazásokhoz
más és más leírási módok lehetnek kényelmesebbek.
\begin{itemize}
\item Egy tengelyekkel párhuzamos téglalapot le lehet (pl.) írni a bal
  felső és jobb alsó sarkával, tehát \pr{téglalap(BalFelső, JobbAlsó)},
  ahol \pr{BalFelső} és \pr{JobbAlsó} pontok: \pr{pont(X, Y)}.  Ha a
  téglalap a tengelyekkel nem párhuzamos, akkor a legegyszerűbb talán
  mind a négy csúcspontot felsorolni (bár ez redundáns).
  Egy másik lehetőség, hogy egy irányított szakasszal és egy hosszal
  írjuk le: \pr{téglalap( szakasz(P1,P2),Hossz)}; ez alapján a téglalap
  úgy szerkeszthető meg, hogy a szakasz
  lerajzolása után derékszögben balra fordulunk,
  és onnan felmérjük a hosszt, a negyedik csúcs pedig már adódik.
\item Egy négyzetet mindig le lehet írni a bal felső és jobb alsó
  sarokkal, de lehet pl.~a középponttal és egy csúcsponttal is;
  tengelyekkel párhuzamos esetben elég a középpont és a csúcsok ettől
  való távolsága is.
\item Egy kört is reprezentálhat a befoglaló négyzete, de megadható a
  középpontja és a sugara által is: \pr{kör(Középpont, Sugár)},
  pl.~\pr{kör(pont(1,2),3)}.
\end{itemize}
\subsubsection*{10.~feladat}
\begin{program}
vízszintes(szakasz(pont(_,Y),pont(_,Y))).
\end{program}
\begin{query}
?- vízszintes(S), függőleges(S).
S = szakasz(pont(_X,_Y),pont(_X,_Y)).
\end{query}
Tehát a ponttá degenerált szakasz ilyen.
\subsubsection*{11.~feladat}
Igen (\pr{A=1,B=2}); nem; nem; igen (\pr{D=2,E=2});
nem;
igen (\pr{P1= pont(-1,0),P2=pont(1,0),P3=pont(0,Y)}).
\subsubsection*{12.~feladat}
A megoldás függ a választott reprezentációtól.
Ha a négy csúcsponttal
írtuk le:
\begin{program}
tengelytéglalap(téglalap(A,B,_,_)) :-
    függőleges(szakasz(P1,P2)).
tengelytéglalap(téglalap(_,B,C,_)) :-
    függőleges(szakasz(P2,P3)).
\end{program}
Ha az irányított szakasz és hossz megoldást
választottuk:
\begin{program}
tengelytéglalap(téglalap(S,_)) :- függőleges(S).
tengelytéglalap(téglalap(S,_)) :- vízszintes(S).
\end{program}
Természetesen sok más megoldás is elképzelhető.
\subsubsection*{13.~feladat}
\begin{program}
kiolvas(1, egy).
kiolvas(2, kettő).
kiolvas(3, három).
\end{program}
\subsubsection*{14.~feladat}
\begin{query}
?- f(s(1), A).
A = kettő.
\end{query}
Csak a második szabállyal egyesíthető.

\begin{query}
?- f(s(s(1)), kettő).
false.
\end{query}
Nincsen egyesíthető szabály.

\begin{query}
?- f(s(s(s(s(s(s(1)))))), C).
C = egy.
\end{query}
Nézzük ezt meg lépésenként.
Az eredeti kérdés csak a negyedik szabállyal egyesíthető;
egyesítés után az
\begin{query}
?- f(s(s(s(1))), C).
\end{query}
kérdést kell megválaszolni, ami ismét a negyedik szabállyal
egyesíthető, tehát:
\begin{query}
?- f(1, C).
\end{query}
Ez pedig az első szabály alapján adja az eredményt.

\begin{query}
?- f(D, három).
D = s(s(1)) ;
D = s(s(s(s(s(1))))) ;
D = s(s(s(s(s(s(s(s(1)))))))) ;
...
\end{query}
A harmadik szabály adja az első megoldást.
A negyedik szabály szerint, ha
\begin{query}
?- f(X, három).
\end{query}
teljesül, akkor \pr{D = s(s(s(X)))}, ez alapján
minden olyan \pr{D} megoldás lesz, ahol $3n+2$ db.~\pr{s}
szerepel.
\subsubsection*{15.~feladat}
\begin{query}
trace, nagy(X), sötét(X), notrace.
Call: nagy(_5078)
Exit: nagy(medve)
Call: sötét(medve)
  Call: fekete(medve)
  Fail: fekete(medve)
Redo: sötét(medve)
  Call: barna(medve)
  Exit: barna(medve)
Exit: sötét(medve)
X = medve 
\end{query}
\subsubsection*{16.~feladat}
A nyomkövetés a \pr{fekete(X)} kielégítésétől indul.
A \pr{notrace} hozzáadásával a nyomkövetés csak addig tart,
amíg el nem jut a \pr{sötét} második szabályához:
\begin{query}
?- sötét(X), nagy(X).
  Call: fekete(_4284)
  Exit: fekete(macska)
Exit: sötét(macska)
Call: nagy(macska)
Fail: nagy(macska)
Redo: sötét(_4284)
X = medve.
\end{query}
\subsubsection*{17.~feladat}
Amikor további bizonyítást keresünk, a második szabály alapján
Fátimának egy másik ősét (nem Mohamedet) fogja keresni,
akiről aztán majd be akarja látni, hogy Abdulla gyermeke.

Ekkor azonban eljutunk a második szabályhoz úgy, hogy az \pr{X}
ismeretlen, és a szabály szerint az
\begin{query}
?- ős3(X, fátima).
\end{query}
kérdés megválaszolásához először meg kell válaszolni az
\begin{query}
?- ős3(Y, fátima).
\end{query}
kérdést -- ez viszont nyilvánvalóan egy végtelen rekurzió.
\subsubsection*{18.~feladat}
\begin{program}
kivesz3(X, Y) :- hozzáfűz(Y, [_,_,_], X).
\end{program}
\subsubsection*{19.~feladat}
\begin{program}
kivesz33(X, Y) :- kivesz3(X, [_,_,_|Y]).
\end{program}
\subsubsection*{20.~feladat}
Hozzáfűzéssel:
\begin{program}
utolsó(X,Y) :- hozzáfűz(_,[Y],X).
\end{program}
Anélkül:
\begin{program}
utolsó(X, [X]). 
utolsó(X, [_|M]) :- utolsó(X, M). 
\end{program}
\subsubsection*{21.~feladat}
\begin{program}
páros_hosszú([]). 
páros_hosszú([_|M]) :- páratlan_hosszú(M). 
páratlan_hosszú([_|M]) :- páros_hosszú(M). 
\end{program}
\subsubsection*{22.~feladat}
\begin{program}
fordított([], []). 
fordított([X|M], F) :-
    fordított(M, L), hozzáfűz(L, [X], F).
\end{program}
\subsubsection*{23.~feladat}
\begin{program}
palindróma(X) :- fordított(X, X).
\end{program}
\subsubsection*{24.~feladat}
\begin{program}
forgat([X|M], Y) :- hozzáfűz(M, [X], Y). 
\end{program}
\subsubsection*{25.~feladat}
\begin{program}
részhalmaz([], []).
részhalmaz([X|M], [X|R]) :- részhalmaz(M, R).
részhalmaz([_|M], R) :- részhalmaz(M, R).
\end{program}
\subsubsection*{26.~feladat}
\begin{program}
ugyanolyan_hosszú([], []). 
ugyanolyan_hosszú([_|M], [_|N]) :-
    ugyanolyan_hosszú(M, N). 
\end{program}
\subsubsection*{27.~feladat}
\begin{program}
lapít([], []). 
lapít([X|M], Y) :-
    lapít(X, X1), lapít(M, M1),
    hozzáfűz(X1, M1, Y). 
lapít(X, [X]) :- X \= [], X \= [_|_]. 
\end{program}
\subsubsection*{28.~feladat}
\begin{query}
?- t(0+1, A).
A = 1+0.
?- t(0+1+1, B).
B = 1+1+0
?- t(1+0+1+1+1, C).
C = 1+1+1+1+0
?- t(D, 1+1+1+0).
D = 1+1+0+1 ;
D = 1+0+1+1 ;
D = 0+1+1+1
\end{query}
\subsubsection*{29.~feladat}
\begin{program}
max(A, B, A) :- A >= B. 
max(A, B, B) :- A < B. 
\end{program}
\subsubsection*{30.~feladat}
\begin{program}
maximum([X], X).
maximum([X,Y|M], Z) :-
    maximum([Y|M], X1), max(X, X1, Z).
\end{program}
\subsubsection*{31.~feladat}
\begin{program}
összeg([], 0). 
összeg([X|M], N):- összeg(M, N1), N is N1 + X. 
\end{program}
\subsubsection*{32.~feladat}
\begin{program}
növekvő([_]). 
növekvő([A,B|M]) :- A < B, növekvő([B|M]). 
\end{program}
\subsubsection*{33.~feladat}
\begin{program}
részösszeg(L, N, X) :-
    részhalmaz(L, X), összeg(X, N).
\end{program}
\subsubsection*{34.~feladat}
\begin{program}
között(A, B, A) :- A =< B. 
között(A, B, X) :-
    A < B, A1 is A + 1,
    között(A1, B, X). 
\end{program}
\subsubsection*{35.~feladat}
\begin{program}
:- op(600, xfx, :=). 
:- op(700, xfx, egyébként). 
:- op(800, xfx, akkor). 
:- op(900, fx, ha). 
ha X > Y akkor Z := V egyébként _ :- X > Y, Z = V. 
ha X > Y akkor _ egyébként Z := V :- X =< Y, Z = V. 
\end{program}
\subsubsection*{36.~feladat}
\begin{query}
?- p(X).
X = 1 ;
X = 2
?- p(X), p(Y).
X = 1, Y = 1 ;
X = 1, Y = 2 ;
X = 2, Y = 1 ;
X = 2, Y = 2
?- p(X), !, p(Y).
X = 1, Y = 1 ;
X = 1, Y = 2
\end{query}
\subsubsection*{37.~feladat}
\begin{program}
szétoszt(_, [], [], []) :- !. 
szétoszt(X, [Y|M], K, [Y|N]) :-
    X =< Y, !, szétoszt(X, M, K, N). 
szétoszt(X, [Y|M], [Y|K], N) :-
    X > Y, !, szétoszt(X, M, K, N). 
\end{program}
\subsubsection*{38.~feladat}
\begin{query}
?- tartalmaz(X, Jelöltek),
   \+ tartalmaz(X, Kiesettek).
\end{query}
\subsubsection*{39.~feladat}
\begin{program}
különbség([], _, []). 
különbség([X|M], Y, Z) :-
    tartalmaz(X, Y), !, különbség(M, Y, Z). 
különbség([X|M], Y, [X|Z]) :- különbség(M, Y, Z).
\end{program}
\subsubsection*{40.~feladat}
\begin{program}
egyesíthető([], _, []) :- !. 
egyesíthető([X|M], Y, L) :-
    \+(X = Y), !, egyesíthető(M, Y, L). 
egyesíthető([X|M], Y, [X|L]) :-
    egyesíthető(M, Y, L). 
\end{program}
Ha tagadás nélkül ellenőriznénk az egyesíthetőséget,
akkor el is végezné az egyesítést:
\begin{program}
egyesíthető_rossz([], _, []) :- !. 
egyesíthető_rossz([X|M], Y, [X|L]) :-
    X = Y, !, egyesíthető_rossz(M, Y, L). 
egyesíthető_rossz([X|M], Y, L) :-
    egyesíthető_rossz(M, Y, L). 
\end{program}
\begin{query}
?- egyesíthető_rossz([X,b,t(Y)], t(a), L).
X = t(a),
Y = a,
L = [t(a), t(a)].
\end{query}
\subsubsection*{41.~feladat}
\begin{program}
katamino(M, Ak) :-
    hossz(Ak, H), N is H * 5 // M,
    H * 5 =:= N * M,
    kirak(N-M, Ak, X), kiír(N-M, X).

kirak(N-M, Ak, X) :- kirak(Ak, N-M, [], X).

kirak([], _, T, T).
kirak(Ak, N-M, T, X) :-
    első_lyuk(N-M, T, P),
    töröl(A, Ak, Ak1),
    letesz(A, P, N-M, T, T1),
    kirak(Ak1, N-M, T1, X).

első_lyuk(N-M, T, X-Y) :-
    között(1, N, X), között(1, M, Y),
    \+ tartalmaz(h(X-Y,_), T), !.

letesz(A, X-Y, N-M, T, T1) :-
    alakzat(A, [_-Dy|Dk]),
    Y1 is Y - Dy, Y1 > 0,
    letesz(A, X-Y1, Dk, N-M, [h(X-Y,A)|T], T1).

letesz(_, _, [], _, T, T).
letesz(A, X-Y, [Dx-Dy|Dk], N-M, T, T1) :-
    X1 is X + Dx, között(1, N, X1),
    Y1 is Y + Dy, között(1, M, Y1),
    \+ tartalmaz(h(X1-Y1,_), T),
    letesz(A, X-Y, Dk, N-M, [h(X1-Y1,A)|T], T1).

kiír(N-M, T) :- kiír(N-M, 1-M, T).

kiír(_, _-0, _).
kiír(N-M, X-Y, T) :-
    Y =< M, X > N, Y1 is Y - 1, nl,
    kiír(N-M, 1-Y1, T).
kiír(N-M, X-Y, T) :-
    Y =< M, X =< N,
    tartalmaz(h(X-Y,A), T),
    write(A), write(' '),
    X1 is X + 1,
    kiír(N-M, X1-Y, T).
\end{program}
(A program további része változatlan.)
\begin{query}
?- katamino(3, [k,p,c,w,l,y,i,r,v,z,+,t]).
c c + i i i i i k k k r t w y y y y z v 
c + + + p p l k k r r r t w w y z z z v 
c c + p p p l l l l r t t t w w z v v v 
true 

?- katamino(4, [k,p,c,w,l,y,i,r,v,z,+,t]).
p p z v v v c c c w w t t t y 
p p z z z v c r c + w w t y y 
p k k k z v r r + + + w t l y 
k k i i i i i r r + l l l l y 
true 

?- katamino(5, [k,p,c,w,l,y,i,r,v,z,+,t]).
c c c r l l l l y y y y 
c k c r r w t l + y z z 
k k r r w w t + + + z v 
k p p w w t t t + z z v 
k p p p i i i i i v v v 
true 

?- katamino(6, [k,p,c,w,l,y,i,r,v,z,+,t]).
p p y y y y z v v v 
p p l l y + z z z v 
p k l w + + + r z v 
k k l w w + r r r t 
k c l c w w r t t t 
k c c c i i i i i t 
true 
\end{query}
\subsubsection*{42.~feladat}
\begin{program}
lapít(X, Y) :- lapít_kl(X, Y-[]).

lapít_kl([], Y-Y).
lapít_kl([X|M], Y-A) :-
    lapít_kl(M, A1-A), lapít_kl(X, Y-A1).
lapít_kl(X, [X|A]-A) :- X \= [], X \= [_|_].
\end{program}
\subsubsection*{43.~feladat}
\begin{program}
holland(X, PFK) :- holland_kl(X, PFK-FK, FK-K, K-[]).

holland_kl([], P-P, F-F, K-K). 
holland_kl([piros(X)|M], [piros(X)|P]-P1, F, K) :-
    holland_kl(M, P-P1, F, K). 
holland_kl([fehér(X)|M], P, [fehér(X)|F]-F1, K) :-
    holland_kl(M, P, F-F1, K). 
holland_kl([kék(X)|M], P, F, [kék(X)|K]-K1) :-
    holland_kl(M, P, F, K-K1). 
\end{program}
\subsubsection*{44.~feladat}
A programban a \pr{k}-végű változónevek listákat
jelölnek, például \pr{Xk} (,,\pr{X}-ek'').%
\footnote{Angolul erre az \pr{s} végződést
használják.}
\begin{program}
egyszerűsít(Kifejezés, Megoldás) :-
    változók(Kifejezés, Változók, Konstansok),
    számol(Változók, Számolt),
    összeg(Konstansok, N),
    felépít(Számolt, N, Megoldás).

összeg([], 0).
összeg([X|Xk], N):- összeg(Xk, N1), N is N1 + X.

felépít([], N, N) :- !.
felépít(Számolt, N, Megoldás) :-
    felépít(Számolt, Érték),
    ( N =:= 0, !, Megoldás = Érték
    ; N =\= 0, Megoldás = Érték + N
    ).

% Felépíti a kifejezés szimbolikus részét
felépít([v(X,1)], X) :- !.
felépít([v(X,K)], K*X) :- K > 1, !.
felépít([V1,V2|Vk], E2+E1) :-
    felépít([V1], E1),
    felépít([V2|Vk], E2).

% változók(+Kifejezés, -Változók, -Konstansok)
%   Szétválogatja a változókat és konstansokat.
változók(X, [X], []) :- atom(X), !.
változók(X, [], [X]) :- number(X), !.
változók(Xk+X, [X|Vk], Kk) :-
    atom(X), !, változók(Xk, Vk, Kk).
változók(Xk+X, Vk, [X|Kk]) :-
    number(X), változók(Xk, Vk, Kk).

% Megszámolja a változók előfordulásait,
% pl. [a,a,b,a,c,b] => [v(a,3),v(b,2),v(c,1)]
számol([], []).
számol([X|Xk], [v(X,N)|Zk]) :-
    számol_és_töröl(X, Xk, N, Yk),
    számol(Yk, Zk).

% számol_és_töröl(+X, +L, -N, -L1)
%   Megszámolja az X előfordulásait L-ben (N),
%   és kitörli őket, ennek eredménye az L1.
számol_és_töröl(_, [], 1, []) :- !.
számol_és_töröl(X, [X|Yk], N, Zk) :-
    !, számol_és_töröl(X, Yk, N1, Zk),
    N is N1 + 1.
számol_és_töröl(X, [Y|Yk], N, [Y|Zk]) :-
    X \= Y, számol_és_töröl(X, Yk, N, Zk).
\end{program}
\subsubsection*{45.~feladat}
\begin{program}
bagof(C, részhalmaz(X, C), L).
\end{program}
\subsubsection*{46.~feladat\footnote{Varga Csilla megoldása.}}
Elég az alábbi szabályokat megváltoztatni.
\begin{program}
% A második szabály egy új kiértékel/3 szabályt hív
játék(Állás, Játékos) :-
    játék_vége(Állás, Játékos, Eredmény), !,
    bejelent(Eredmény).
játék(Állás, Játékos) :-
    lépést_választ(Állás, Játékos, Lépés),
    kiértékel(Lépés, Állás, Játékos).

kiértékel(kilépés, _, _) :-
    kiír('Viszlát!').
kiértékel(Lépés, Állás, Játékos) :-
    érvényes(Lépés), !,
    lép(Lépés, Állás, Állás1),
    következő_játékos(Játékos, Játékos1), !,
    kirajzol(Állás1, Játékos1),
    játék(Állás1, Játékos1).
kiértékel(_, Állás, Játékos) :-
    kiír('Érvénytelen lépés. Próbáld újra!'),
    játék(Állás, Játékos).

% Itt nincs szükség az érvényesség ellenőrzésére
% (a lépés_választ/3 másik szabálya változatlan).
lépést_választ(_, ember, Lépés) :-
    nl, kiír('Melyiket választod?'),
    read(Lépés).
\end{program}
\subsubsection*{47.~feladat\footnote{Varga Csilla megoldása.}}
A trükk az, hogy az érvényesség ellenőrzése helyett
ténylegesen elvégezzük a lépést, és ha sikerül,
akkor jó, ha nem, akkor hibás.
A változtatások az előző feladathoz képest
értendők; csak a \pr{kiértékel/3} második szabálya
módosult.
\begin{program}
kiértékel(Lépés, Állás, Játékos) :-
    lépés(Állás, Lépés), !,
    lép(Lépés, Állás, Állás1), 
    következő_játékos(Játékos, Játékos1), !,
    kirajzol(Állás1, Játékos1),
    játék(Állás1, Játékos1).
\end{program}
\subsubsection*{48.~feladat\footnote{Varga Csilla megoldása alapján.}}
Itt a legtöbb problémát annak a kezelése okozza,
hogy a kezdő lyukat az esetleges visszatérő vetés
során ki kell hagyni. Ennek érdekében megváltoztatjuk
a reprezentációt, és sorszámozzuk a lyukakat.

Szintén érdekes annak a biztosítása, hogy ha lehet,
akkor hagyjunk követ az ellenfél oldalán. Ehhez
megvizsgálunk minden lépéslehetőséget, és csak akkor
hagyunk meg olyanokat, amelyek nem hagynak követ
az ellenfélnek, ha nincsen más.

A változtatások az előző feladathoz képest értendők.
\begin{program}
kövek_száma(4).

% A lyukak meg vannak sorszámozva.
alapbeállítás(Állás, ember) :-
    kövek_száma(K),
    Állás = tábla([1-K,2-K,3-K,4-K,5-K,6-K],0,
                  [1-K,2-K,3-K,4-K,5-K,6-K],0).

% Hozzáad/levesz sorszámokat.
sorszámoz([K1,K2,K3,K4,K5,K6],
          [1-K1,2-K2,3-K3,4-K4,5-K5,6-K6]).

% Kezeli a sorszámokat.
kövek(I, tábla(L,_,_,_), K) :-
    n_edik(I, L, _-K), K > 0.

% A táblákból ki kell venni a sorszámokat
% kirajzolás előtt.
kirajzol(tábla(La,Na,Lb,Nb)) :-
    nl,
    sorszámoz(La1, La),
    fordított(La1, F),
    sort_ír(F),
    kalahot_ír(Na, Nb),
    sorszámoz(Lb1, Lb),
    sort_ír(Lb1).

% Figyel arra, hogy csak akkor hagyja üresen az
% ellenfél térfelét, ha nem tehet mást.
lépés(Állás, X) :-
   findall(L, lehetőség(Állás, L), Lépések),
   szétválaszt(Lépések, Üres, Nemüres),
   ( Nemüres = [] -> tartalmaz(X-_, Üres)
   ; tartalmaz(X-_, Nemüres)
   ).

% Megfelel a régi lépés/2 szabálynak,
% de nincsen sosem a lépésnek folytatása,
% és nincsen szükség az üres tábla esetre sem.
lehetőség(Állás, L-Állás1) :-
    tartalmaz(L, [1,2,3,4,5,6]),
    kövek(L, Állás, _),
    lép(L, Állás, Állás1).

% Különválogatja azokat a lépéseket, amelyek
% üresen hagyják az ellenfél térfelét azoktól,
% amelyek nem.
szétválaszt([], [], []).
szétválaszt([L-A|M], [L-A|Üres], Nemüres) :-
    A = tábla(_,_,Lb,_), sorszámoz(Lb1, Lb),
    üres(Lb1), !,
    szétválaszt(M, Üres, Nemüres).
szétválaszt([L-A|M], Üres, [L-A|Nemüres]) :-
    szétválaszt(M, Üres, Nemüres).

% A lépés nem lista, mert nincsen ismételt lépés.
lép(L, Állás, Állás2) :- 
    kövek(L, Állás, K),
    vet(K, L, L, Állás, tábla(La,Na,Lb,Nb)),
    söpör(tábla(La,Na,Lb,Nb), tábla(La1,Na1,Lb,Nb)),
    megfordít(tábla(La1,Na1,Lb,Nb), Állás2).  

% Ha kiürült az ellenfél oldala, minden a miénk.
söpör(tábla(La,Na,Lb,Nb), tábla(Lb,Na1,Lb,Nb)) :-
    sorszámoz(Lb1, Lb),
    üres(Lb1), !,
    sorszámoz(La1, La),
    összeg(La1, X),
    Na1 is Na + X.
söpör(Y, Y).

% A kiinduló lyukat (KLuk) is megkapja,
% hogy azt ki tudjuk hagyni.
vet(Kövek, Luk, KLuk, Állás, Állás2) :-
    vet_saját(Kövek, Luk, KLuk, Állás,
              Állás1, Kövek1),
    vet_ellenfél(Kövek1, KLuk, Állás1, Állás2).

% Amikor már nem először vetünk a saját oldalon,
% akkor a kihagyott kezdő lyuk miatt
% eggyel kevesebb kő kell.
vet_saját(Kövek, Luk, KLuk, tábla(La,Na,Lb,Nb),
          tábla(La1,Na,Lb,Nb), Kövek1) :-
    ( Luk = KLuk -> N = 6 ; N = 5 ),
    Kövek > N - Luk, !,
    felvesz_és_szór(Luk, KLuk, Kövek, La, La1),
    Kövek1 is Kövek + Luk - N.
vet_saját(Kövek, Luk, KLuk, tábla(La,Na,Lb,Nb),
          tábla(La1,Na,Lb,Nb), 0) :-
    felvesz_és_szór(Luk, KLuk, Kövek, La, La1).

% Lényegében változatlan, csak kezeli a sorszámokat,
% és átadja a kiindulási lukat a szór/4 szabálynak.
felvesz_és_szór(0, KLuk, K, L, L1) :-
    !, szór(K, KLuk, L, L1).
felvesz_és_szór(1, KLuk, K, [X-_|M], [X-0|M1]) :-
    !, szór(K, KLuk, M, M1).
felvesz_és_szór(Luk, KLuk, K, [L|M], [L|M1]) :-
    Luk > 1, !, Luk1 is Luk - 1,
    felvesz_és_szór(Luk1, KLuk, K, M, M1).

% Figyel arra, hogy kihagyja a kiinduló lukat.
szór(0, _, L, L) :- !.
szór(N, KLuk, [X-L|M], [X-L|M1]) :-
    N > 0, X = KLuk, !,
    szór(N, KLuk, M, M1).
szór(N, KLuk, [X-L|M], [X-L1|M1]) :-
    N > 0, X \= KLuk, !,
    N1 is N - 1, L1 is L + 1,
    szór(N1, KLuk, M, M1).
szór(_, _, [], []) :- !.

% Ha az ellenfél oldalán fejeződik be a vetés,
% akkor ellenőrzi, hogy elfogunk-e köveket.
vet_ellenfél(0, _, Állás, Állás) :- !.
vet_ellenfél(Kövek, _, tábla(La,Na,Lb,Nb),
             tábla(La, Na2, Lb2, Nb)) :-
    1 =< Kövek, Kövek =< 6,
    szór_ellenfél(Kövek, Lb, Lb1),
    elfog(Kövek, Lb1, Na, Lb2, Na2).
vet_ellenfél(Kövek, KLuk,
             tábla(La,Na,Lb,Nb), Állás) :-
    Kövek > 6, !, Kövek1 is Kövek - 6,
    szór_ellenfél(6, Lb, Lb1),
    vet(Kövek1, 0, KLuk, tábla(La,Na,Lb1,Nb), Állás).

% Ez megegyezik a régi szór/3 szabállyal,
% csak kezeli a sorszámokat is.
szór_ellenfél(0, L, L) :- !.  
szór_ellenfél(N, [X-L|M], [X-L1|M1]) :-
    N > 0, !,
    N1 is N - 1, L1 is L + 1,
    szór_ellenfél(N1, M, M1).
szór_ellenfél(_, [], []) :- !.

% Megfordítja az ellenfél köveinek listáját,
% hogy könnyen végig lehessen menni rajta
% rekurzióval, majd a végén a módosult listát
% visszafordítja.
elfog(Kövek, Lb1, Na, Lb2, Na2) :-
    fordított(Lb1, Lb1f),
    fog(Kövek, Lb1f, Na, Lb2f, Na2),
    fordított(Lb2f, Lb2).

% Megkeresi a lyukat, ahová az utolsó kő esett,
% és ha ez nem 2 vagy 3, akkor nem történik semmi.
% Egyébként a saját kalahjába teszi, és megy tovább
% a lyukakon (visszafelé, mert a lyukak listája meg
% lett fordítva).
fog(_, [], Na, [], Na) :- !.
fog(Kövek, [N-X|M], Na, [N-X|M1], Na2) :-
    N > Kövek, !, fog(Kövek, M, Na, M1, Na2).
fog(Kövek, [N-X|M], Na, [N-X|M], Na) :-
    N = Kövek, ( X < 2 ; X > 3 ), !.
fog(Kövek, [N-X|M], Na, [N-0|M1], Na2) :-
    N = Kövek, ( X =:= 2 ; X =:= 3 ),
    Na1 is Na + X, Kövek1 is Kövek - 1,
    fog(Kövek1, M, Na1, M1, Na2).
\end{program}
\subsubsection*{49.~feladat}
(Megoldás nélkül.)
\subsubsection*{50.~feladat}
Ld.~az \name{SWI-Prolog} \pr{heaps} modulját.
